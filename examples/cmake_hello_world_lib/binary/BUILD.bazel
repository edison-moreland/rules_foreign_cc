# example code is taken from https://github.com/Akagi201/learning-cmake/tree/master/hello-world-lib
# for test only
load("@bazel_tools//tools/build_rules:test_rules.bzl", "file_test")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake", "runnable_bin")

filegroup(
    name = "srcs",
    srcs = glob(["**"]),
    visibility = ["//visibility:public"],
)

cmake(
    name = "libhello",
    # Probably this variable should be set by default.
    # Apparently, it needs to be set for shared libraries on Mac OS
    cache_entries = {
        "CMAKE_MACOSX_RPATH": "True",
    },
    lib_source = ":srcs",
    out_binaries = select({
        "//:windows": ["hello.exe"],
        "//conditions:default": ["hello"],
    }),
    out_interface_libs = select({
        "//:windows": ["libhello.lib"],
        "//conditions:default": [],
    }),
    out_shared_libs = select({
        "//:macos": ["libhello.dylib"],
        "//:windows": ["libhello.dll"],
        "//conditions:default": ["libhello.so"],
    }),
)

filegroup(
    name = "binary",
    srcs = [":libhello"],
    output_group = select({
        "//:windows": "hello.exe",
        "//conditions:default": "hello",
    }),
)

genrule(
    name = "call_hello",
    srcs = [],
    outs = ["out.txt"],
    cmd = "./$(location :binary) me > $(location out.txt)",
    tools = [":binary"],
)

file_test(
    name = "test_binary",
    content = "Hello, me!",
    file = ":call_hello",
)

runnable_bin(
    name = "runnable_hello",
    binary = select({
        "//:windows": "hello.exe",
        "//conditions:default": "hello",
    }),
    foreign_cc_target = "libhello"
)
